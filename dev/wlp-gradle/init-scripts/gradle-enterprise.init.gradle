/*******************************************************************************
 * Copyright (c) 2019 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-2.0/
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

/**
 * Apply Gradle Enterprise plugin.
 * See: https://docs.gradle.com/build-scan-plugin/
 */
import org.gradle.util.GradleVersion
import org.gradle.api.initialization.Settings

def pluginVersion = "3.13.2"
def isTopLevelBuild = gradle.getParent() == null

if (isTopLevelBuild) {
    def gradleVersion = GradleVersion.current().baseVersion
    def atLeastGradle6 = gradleVersion >= GradleVersion.version("6.0")

    if (atLeastGradle6) {
        beforeSettings { settings ->
            configureBuildScript(settings, pluginVersion)
        }
        settingsEvaluated {
            if (!it.pluginManager.hasPlugin("com.gradle.enterprise")) {
                it.pluginManager.apply("com.gradle.enterprise")
            }
            configureExtension(it.extensions["gradleEnterprise"])
        }
    }
}

void configureBuildScript(Settings settings, String pluginVersion) {
    settings.buildscript.repositories { gradlePluginPortal() }
    settings.buildscript.dependencies.classpath("com.gradle:gradle-enterprise-gradle-plugin:${pluginVersion}")
}

void configureExtension(extension) {
    def acceptFile = new File(gradle.gradleUserHomeDir, "build-scans/open-liberty/gradle-scans-license-agree.txt")
    boolean isCiServer = System.getenv().containsKey("CI")
    boolean hasAccepted = isCiServer || acceptFile.exists() && acceptFile.text.trim() == 'yes'
    extension.with {
        buildScan {
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            if (hasAccepted) {
                termsOfServiceAgree = "yes"
            }
        }
    }
}
